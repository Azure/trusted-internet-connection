AzureDiagnostics 
| where Category == 'AzureFirewallNetworkRule' or Category == 'AzureFirewallApplicationRule' 
| where TimeGenerated > ago(30m) 
| parse msg_s with Protocol1 ' request from ' SourceIP1 ':' SourcePortInt: int ' to ' TargetIP1 ':' TargetPortInt: int* 
| parse msg_s with *'. Action: ' Action1a | parse msg_s with *' was ' Action1b ' to ' NatDestination 
| parse msg_s with Protocol2 ' request from ' SourceIP2 ' to ' TargetIP2 '. Action: ' Action2 
| extend
    SourceIP = 
        iif(isnotempty(SourceIP1), 
            tostring(SourceIP1), 
            tostring(SourceIP2)
        ),
    SourcePort = 
        iif(isnotempty(SourcePortInt), 
            tostring(SourcePortInt),
            'N/A'
        ),
    TargetPort = 
        iif(isnotempty(TargetPortInt), 
             tostring(TargetPortInt),
            'N/A'
        ),
    TargetIP = 
        iif(isnotempty(TargetIP1), 
            tostring(TargetIP1), 
            tostring(TargetIP2)
        ),
    Protocol = 
        iif(isnotempty(Protocol1), 
            tostring(Protocol1), 
            tostring(Protocol2)
        ),
    Action = 
        iif(isnotempty(Action1a), 
            replace_string(tostring(Action1a), '.',''), 
            iif(isnotempty(Action1b), 
                tostring(Action1b),
                tostring(Action2)
            )
        )
| project 
    Category,
    TimeGenerated,
    Protocol,
    SourceIP,
    SourcePort,
    TargetIP,
    TargetPort,
    Action